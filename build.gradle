plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = 'com.bitaspire'
version = '1.1.2'

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://oss.sonatype.org/content/groups/public/')
    }

    maven {
        url = uri('https://repo.extendedclip.com/content/repositories/placeholderapi/')
    }

    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://croabeast.github.io/repo/')
    }
}

dependencies {
    // Spigot API
    compileOnly "org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT"

    // Core dependency
    implementation files('libs/CyberCore.jar')

    // Lombok
    compileOnly "org.projectlombok:lombok:1.18.38"
    annotationProcessor "org.projectlombok:lombok:1.18.38"

    // Other plugin APIs
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly files('libs/RivalHarvesterHoesAPI.jar')
    compileOnly files('libs/RivalPickaxesAPI.jar')

    // Database
    compileOnly 'com.zaxxer:HikariCP:7.0.2'
    compileOnly 'com.mysql:mysql-connector-j:9.5.0'
    compileOnly 'org.xerial:sqlite-jdbc:3.51.1.0'
    compileOnly 'org.postgresql:postgresql:42.7.8'

    // Misc utils
    implementation 'me.croabeast.expr4j:core:1.0'
    implementation 'me.croabeast.expr4j:big-decimal:1.0'
    implementation 'me.croabeast.expr4j:double:1.0'

    compileOnly 'ch.obermuhlner:big-math:2.3.2'
    compileOnly 'org.apache.commons:commons-lang3:3.18.0'
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.build.dependsOn(tasks.shadowJar)

tasks {
    shadowJar {
        archiveClassifier.set('')
        exclude('META-INF/**', 'org/apache/commons/**', 'org/intellij/**', 'org/jetbrains/**', 'me/croabeast/file/plugin/YAMLPlugin.*')
    }
}